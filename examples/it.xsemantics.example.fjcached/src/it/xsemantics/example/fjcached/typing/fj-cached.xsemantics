import it.xsemantics.example.fj.fj.Class
import it.xsemantics.example.fj.fj.Expression
import it.xsemantics.example.fj.fj.Field
import it.xsemantics.example.fj.fj.Method
import it.xsemantics.example.fj.fj.Type
import java.util.List
import it.xsemantics.example.fj.typing.FjTypeSystem

/*
 * A version of the FJ system where caching is used.
 */
system it.xsemantics.example.fjcached.typing.FjCachedTypeSystem extends FjTypeSystem

auxiliary {
	override superclasses(Class cl) : List<Class> cached
	override fields(Class cl) : List<Field> cached
	override methods(Class cl) : List<Method> cached
}

judgments {
	override type |- Expression expression : output Type cached
		error "cannot type " + stringRep(expression)
		source expression
	override subtype |- Type left <: Type right cached
		error stringRep(left) + " is not a subtype of " + stringRep(right)
	override equalstype |- Type left ~~ Type right cached
		error stringRep(left) + " is not the same type as " + stringRep(right)
}
